plugins {
    id "maven-publish"
    alias(libs.plugins.shadow)
}

version = "2.9.7"
description = "A lightweight yet very powerful hologram plugin with many features and configuration options."

dependencies {
    implementation project(":core")
    implementation project(":expansion")
}

processResources {
    def props = [version: project.version, description: project.description]
    inputs.properties(props)
    filteringCharset = "UTF-8"
    filesMatching("plugin.yml") {
        expand(props)
    }
}

jar {
    manifest {
        attributes "Multi-Release": "true"
    }
}

shadowJar {
    archiveFileName.set("DecentHolograms-${version}.jar")

    relocate "org.bstats", "eu.decentsoftware.holograms.metrics"
    relocate "de.tr7zw.changeme.nbtapi", "eu.decentsoftware.holograms.libs.nbtapi"
    relocate "com.cryptomorin.xseries", "eu.decentsoftware.holograms.libs.xseries"

    mergeServiceFiles()
}

publishing {
    publications {
        create("shadow", MavenPublication) {
            from(components["java"] as SoftwareComponent)
            artifactId = "decentholograms"
        }
    }
}
